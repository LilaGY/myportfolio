//import sound library in order to play audio files
//+ declare sound variables
import processing.sound.*;
SoundFile youpi, theme;

//Declare Spaceship (monkeys)
Spaceship s1;

//Declare Laser
Laser l1;

//Declare aliens
Aliens[] aliens = new Aliens[30];

//declare monkey's and lazer's positions
float ssX, ssY, lsX, lsY;
int counter, sec;

//startGame boolean variable, true when mouse
//first click happens
boolean startGame,hit;

//declare ints
int seuilnuage=300;
int nbsinge=3;
int nbnuage=10;
int nbbanane=2;

//declare clouds positions arrays
float [] nuageX = new  float[nbnuage] ;
int [] nuageY = new int[nbnuage] ;

//declare monkeys and bananas
int n = int(random(0,3));
PImage [] singe = new PImage[nbsinge];
PImage [] banane = new PImage[nbbanane];
//declare background images
PImage ciel;
PImage temple;
PImage nuage;
PImage cup;

//First launched function, called only once
void setup() {
  noCursor();
  counter = 0;
  startGame = false;
  
  //define frame size
  size(500,700);
  background(0);
  
  //load pictures
  banane[0] = loadImage("banane.png");
  banane[1] = loadImage("banane2.png");
  singe[1] = loadImage("singe.png");
  singe[2] = loadImage("singe2.png");
  singe[0] = loadImage("singe3.png");
  ciel = loadImage("ciel.png");
  temple = loadImage("temple.png");
  nuage = loadImage("nuage.png");
  cup = loadImage("cup.png");
  
  //load sounds
  youpi = new SoundFile(this, "youpi.wav");
  theme = new SoundFile(this, "theme.mp3");
  
  //play main theme
  theme.play();
  
  //Pop clouds
  for (int i=0; i<nbnuage;i++){
    nuageX[i]=random(-200,550);
    nuageY[i]=int(random(0,seuilnuage));
  }
 
  //new instance of Laser and Shipspace class
  l1 = new Laser();
  s1 = new Spaceship();
  
  //Pop aliens
  for (int i=0;i<15;i++) { // lvl 2 aliens
    aliens[i] = new Aliens((20+80*(i%5)),(40+floor(i/5)*40)-400,2);
  }
  for (int i=15;i<30;i++) { //lvl 1 aliens
    aliens[i] = new Aliens((20+80*((i-15)%5)),(40+floor((i-15)/5)*40),1);
  }
}


//Second launched function, 
//looped while the program runs
void draw() {
  
  //draw sky and temple 
  image(ciel, 0, 0);
  image(temple, -12, 426);
  
  //draw clouds
  for (int i=0; i<nbnuage;i++){
    image(nuage,nuageX[i],nuageY[i],100,50);
  }
  
  //define framerate
  frameRate(60);
  
  //background(0);
  sec = millis()-1000*floor(millis()/1000);
  
  //make aliens move
  for (int i=0;i<aliens.length;i++) {
    aliens[i].display();
    aliens[i].travel();
    aliens[i].collision();
  
    //only way i found to draw clouds under aliens.
    //modulo condition is intended to make the refresh
    //slower
    if (i % 10000000 == 0) {
      redraw_nuages();
    }
    
    aliens[i].gameover();
  }
  
  // lazer and spaceship display (n being a 
  // random monkey sprite generated by new_singe
  // function when mouse is clicked -> firing lazers)
  s1.steer();
  l1.display(n);
  l1.shoot();
  s1.display(n);
  new_singe();
}

//random new monkey 
void new_singe(){
  //when mouse is pressed = lazer is fired
  if (mousePressed == true){
      n = int(random(0,3)); 
      s1.display(n);
      }
}

//change clouds positions and redraw them 
//clouds are moving at various speeds
void redraw_nuages(){
  for (int i=0; i<nbnuage; i++){
    //if cloud get out of the frame 
    if (nuageX[i]>500){
    nuageX[i] = -150;
    }
    else if ((nuageX[i]<500) && (i % 3 == 0)){
      nuageX[i] += 1;
    }
    else if ((nuageX[i]<500) && (i % 2 == 0)){
      nuageX[i] += 0.6;
    }
    else{
      nuageX[i] += 0.3;
    }
  }
  //redraw
  for (int i=0; i<nbnuage;i++){
    image(nuage,nuageX[i],nuageY[i],100,50);
  } 
}
  